include(${PROJECT_SOURCE_DIR}/CMake/Skyrocket/platform.cmake)
include(${PROJECT_SOURCE_DIR}/CMake/Skyrocket/graphics.cmake)

function(skyrocket_init)
    unset(skyrocket_sources CACHE)
    unset(skyrocket_libraries CACHE)
    unset(skyrocket_definitions CACHE)
    unset(skyrocket_build_type CACHE)
    unset(skyrocket_graphics_api CACHE)

    if (CMAKE_BUILD_TYPE STREQUAL "Debug")
        skyrocket_add_definitions(-DSKY_DEBUG)
    else()
        skyrocket_add_definitions(-DSKY_RELEASE)
    endif ()
endfunction()

# Adds source files
function(skyrocket_add_sources)
    set(srcs)
    foreach(s IN LISTS ARGN)
        if (NOT IS_ABSOLUTE "${s}")
            get_filename_component(s "${s}" ABSOLUTE)
        endif ()
        list(APPEND srcs "${s}")
    endforeach()

    set(skyrocket_sources ${skyrocket_sources} "${srcs}" CACHE INTERNAL "")
endfunction()

function(skyrocket_add_library lib_name lib_type)
    add_library(${lib_name} "${lib_type}" "${skyrocket_sources}")
    target_include_directories(${lib_name} PUBLIC ${PROJECT_SOURCE_DIR}/Source)
    set(${lib_name}_sources "${skyrocket_sources}" CACHE INTERNAL "")
    set(skyrocket_libraries ${skyrocket_libraries} "${lib_name}" CACHE INTERNAL "")
    skyrocket_include_all_definitions()
    unset(skyrocket_sources CACHE)
endfunction()

function(skyrocket_new_module module_name)
    set(${module_name}_sources "${skyrocket_sources}" CACHE INTERNAL "")
    unset(skyrocket_sources CACHE)
endfunction()

function(skyrocket_add_definitions)
    foreach (def IN LISTS ARGN)
        add_definitions(${def})
        set(skyrocket_definitions ${skyrocket_definitions} "${def}" CACHE INTERNAL "")
    endforeach ()
endfunction()

function(skyrocket_include_all_definitions)
    foreach(def IN LISTS skyrocket_definitions)
        add_definitions("${def}")
    endforeach()
endfunction()

